BACKUP ~weidu_external/zgmagic/backup~ // location to store files for uninstall purposes
AUTHOR ~Graion Dilach~
VERSION ~0.01~


LANGUAGE ~English~ ~english~
	~zgmagic/english/setup.tra~


BEGIN @100
LABEL ZG-MAGIC-SI4SPELLREV
DESIGNATED 100
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee iwdee eet~) @10
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~dvimhere.mrk~) @101

	// ClassSpellTool this back, to decrease the amount of conflicts this can introduce and to prevent the need to use a SPELL.IDS slot
	// I mean, Spell Immunity doesn't exist in SR, so yeah, it shouldn't have such

	INCLUDE ~%MOD_FOLDER%/ClassSpellTool.TPA~
	
	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW510.2DA~ ~override~
	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW510A.BAM~ ~override~
	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW510B.BAM~ ~override~
	

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW510.SPL~ ~override~
		SAY NAME1 @102
		SAY UNIDENTIFIED_DESC @103
	

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW590.SPL~ ~override~
		SAY NAME1 @104

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW591.SPL~ ~override~
		SAY NAME1 @105

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW592.SPL~ ~override~
		SAY NAME1 @106

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW593.SPL~ ~override~
		SAY NAME1 @107

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW594.SPL~ ~override~
		SAY NAME1 @108

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW595.SPL~ ~override~
		SAY NAME1 @109

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW596.SPL~ ~override~
		SAY NAME1 @110

	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW597.SPL~ ~override~
		SAY NAME1 @111
	
	COPY ~%MOD_FOLDER%/si4spellrev/ZGMW510S.ITM~ ~override~
		SAY NAME1 @112
		SAY NAME2 @102
		SAY DESC @103

	LAF ADD_CLASS_SPELL STR_VAR resref=~ZGMW510~ scroll=~ZGMW510S~ class_include=~{202, 5, 19}~ END

	COPY_EXISTING_REGEXP GLOB ~.*\.sto~ ~override~
		READ_LONG 0x034 ofs_items_for_sales
		READ_LONG 0x038 num_items_for_sales
		FOR (idx = 0; idx < num_items_for_sales; ++idx) BEGIN
	  		SET offset = ofs_items_for_sales + idx * 0x1c
	  		READ_ASCII offset itemname (8) NULL
	  		PATCH_IF (~%itemname%~ STR_EQ ~SCRL6S~) BEGIN
				ADD_STORE_ITEM ~ZGMW510S~ AFTER ~SCRL6S~ #0 #0 #0 ~IDENTIFIED~ #2
			END
		END
	BUT_ONLY

BEGIN @115
LABEL ZG-MAGIC-TRUESIGHT4SPELLREV
DESIGNATED 115
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee iwdee eet~) @10
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~dvimhere.mrk~) @101

	// ClassSpellTool this back, to decrease the amount of conflicts this can introduce and to prevent the need to use a SPELL.IDS slot
	// I mean, True Sight affects balance heavily and it's not what SR expects

	INCLUDE ~%MOD_FOLDER%/ClassSpellTool.TPA~
	
	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEA.BAM~ ~override~
	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEB.BAM~ ~override~

	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEI.SPL~ ~override~
		SAY NAME1 @116
		SAY UNIDENTIFIED_DESC @117
	
	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEP.SPL~ ~override~
		SAY NAME1 @116
		SAY UNIDENTIFIED_DESC @118
	
	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEW.SPL~ ~override~
		SAY NAME1 @116
		SAY UNIDENTIFIED_DESC @119
	
	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEID.SPL~ ~override~
		SAY NAME1 @116
		SAY UNIDENTIFIED_DESC @117
	
	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEPD.SPL~ ~override~
		SAY NAME1 @116
		SAY UNIDENTIFIED_DESC @118
	
	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEWD.SPL~ ~override~
		SAY NAME1 @116
		SAY UNIDENTIFIED_DESC @119
	
	COPY ~%MOD_FOLDER%/ts4spellrev/ZGMEYEWS.ITM~ ~override~
		SAY NAME1 @112
		SAY NAME2 @116
		SAY DESC @119

	// add priest
	LAF ADD_CLASS_SPELL STR_VAR resref=~ZGMEYEP~ class_include=~{204, 208, 3, 11, 21}~ END

	// add mage
	LAF ADD_CLASS_SPELL STR_VAR resref=~ZGMEYEW~ scroll=~ZGMEYEWS~ class_include=~{202, 5, 19}~ END

	// add to shops where Mass Invisibility is already available
	COPY_EXISTING_REGEXP GLOB ~.*\.sto~ ~override~
		READ_LONG 0x034 ofs_items_for_sales
		READ_LONG 0x038 num_items_for_sales
		FOR (idx = 0; idx < num_items_for_sales; ++idx) BEGIN
	  		SET offset = ofs_items_for_sales + idx * 0x1c
	  		READ_ASCII offset itemname (8) NULL
	  		PATCH_IF (~%itemname%~ STR_EQ ~SCRL8W~) BEGIN
				ADD_STORE_ITEM ~ZGMEYEWS~ AFTER ~SCRL8W~ #0 #0 #0 ~IDENTIFIED~ #2
			END
		END
	BUT_ONLY

	// add inquisitor
	APPEND ~CLABPA04.2DA~ ~ZGTRUEVISION    **** ****  ****  ****  ****  **** **** **** GA_ZGMEYEI ****        ****  ****        ****  ****        ****  **** GA_ZGMEYEI  ****  ****        ****        ****  ****  ****        ****        GA_ZGMEYEI  ****  ****        ****        ****  ****        ****  ****        GA_ZGMEYEI  ****        ****        ****  ****  ****        ****        GA_ZGMEYEI~

	COPY_EXISTING ~CLABPA04.2DA~ ~override~
		// Ensure all table rows have valid number of columns.
		// Missing headers are padded by column indices. Missing content is padded by default value.
		SET num_added = 0
		COUNT_2DA_COLS num_cols
		READ_2DA_ENTRIES_NOW table 1
		READ_2DA_ENTRY_FORMER table 1 0 default
		FOR (row = 0; row < table; ++row) BEGIN
			READ_2DA_ENTRY_FORMER table row 0 line
			FOR (col = 1; col < num_cols && VARIABLE_IS_SET $table(~%row%~ ~%col%~); ++col) BEGIN
				READ_2DA_ENTRY_FORMER table row col v
				TEXT_SPRINT line ~%line% %v%~
			END
			// fill in missing data
			PATCH_IF (row = 2) BEGIN  // header
				FOR (; col < num_cols - 1; ++col) BEGIN
					TEXT_SPRINT line ~%line% %col%~
					SET num_added += 1
				END
			END ELSE PATCH_IF (row > 2) BEGIN // content
				FOR (; col < num_cols; ++col) BEGIN
					TEXT_SPRINT line ~%line% %default%~
					SET num_added += 1
				END
			END
			TEXT_SPRINT $lines(~%row%~) ~%line%%LNL%~
			SET $lines(~%row%~ ~size~) = STRING_LENGTH $lines(~%row%~)
		END
	  
		// write normalized table to buffer
		PATCH_IF (num_added > 0) BEGIN
			DELETE_BYTES 0 BUFFER_LENGTH
			FOR (row = 0 pos = 0; row < table; ++row) BEGIN
				SET size = $lines(~%row%~ ~size~)
				INSERT_BYTES pos size
				WRITE_ASCIIE pos $lines(~%row%~) (size)
				SET pos += size
			END
		END
		PRETTY_PRINT_2DA
	BUT_ONLY

BEGIN @120
LABEL ZG-MAGIC-SR-MAXIMIZE-CURE-WOUNDS
DESIGNATED 120
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee iwdee eet~) @10
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~dvimhere.mrk~) @101

	INCLUDE ~%MOD_FOLDER%/lib/library.tpa~

	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~CLERIC_CURE_LIGHT_WOUNDS~
		RET spell_num = spell_num spell = spell_res
	END

	COPY_EXISTING ~%spell%.spl~ ~override~
		LPM ADD_DICES_TO_OP17
		SAY UNIDENTIFIED_DESC @121
	BUT_ONLY
	
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~CLERIC_CURE_MEDIUM_WOUNDS~
		RET spell_num = spell_num spell = spell_res
	END

	COPY_EXISTING ~%spell%.spl~ ~override~
		LPM ADD_DICES_TO_OP17
		SAY UNIDENTIFIED_DESC @123
	BUT_ONLY

	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~CLERIC_CURE_SERIOUS_WOUNDS~
		RET spell_num = spell_num spell = spell_res
	END

	COPY_EXISTING ~%spell%.spl~ ~override~
		LPM ADD_DICES_TO_OP17
		SAY UNIDENTIFIED_DESC @124
	BUT_ONLY

/* would be Cure Mortal Wounds in SR, but it's disabled
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~CLERIC_CURE_CRITICAL_WOUNDS~
		RET spell_num = spell_num spell = spell_res
	END

	COPY_EXISTING ~%spell%.spl~ ~override~
		LPM ADD_DICES_TO_OP17
		SAY UNIDENTIFIED_DESC @124
	BUT_ONLY
*/
	
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~INNATE_CURE_LIGHT_WOUNDS~
		RET spell_num = spell_num spell = spell_res
	END

	COPY_EXISTING ~%spell%.spl~ ~override~
		LPM ADD_DICES_TO_OP17
		SAY UNIDENTIFIED_DESC @121
	BUT_ONLY
	
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~ULITHARID_CURE_SERIOUS_WOUNDS~
		RET spell_num = spell_num spell = spell_res
	END

	COPY_EXISTING ~%spell%.spl~ ~override~
		LPM ADD_DICES_TO_OP17
		SAY UNIDENTIFIED_DESC @124
	BUT_ONLY

	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~CLERIC_CURE_MODERATE_WOUNDS~
		RET spell_num = spell_num spell = spell_res
	END

	COPY_EXISTING ~%spell%.spl~ ~override~
		LPM ADD_DICES_TO_OP17
		SAY UNIDENTIFIED_DESC @122
	BUT_ONLY


BEGIN @125
LABEL ZG-CWE-4-SRnIWD-SCS
DESIGNATED 125
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee iwdee eet~) @10
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~dvimhere.mrk~) @126
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~CWELEMW.2DA~) @126

// When SR's installed before the IWD spells, the IWD library's elemental_summoning function never runs. However,
// because this is the function which creates the Conjure Water Elemental spell, this will be missing from the IDS
// list, which then leads to Not_found errors when SCS's AI compoonents are installed with the IWD spell lists.
// This component installs static variants of the new elementals, following SR rules on the spell themselves.
// Needs to be ADD_SPELLed for SCS to detect it.

	//install IWD2 water elemental art (only large exists in SoD)
	COPY ~%MOD_FOLDER%/srwaterelem4scs/art~ ~override~

	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMCWEA.BAM~ ~override~
	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMCWEB.BAM~ ~override~
	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMCWEC.BAM~ ~override~
	
	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMWAE.ITM~ ~override~

	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMWAE.EFF~ ~override~
	
	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMGWAE.EFF~ ~override~
	
	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMWAE.CRE~ ~override~
		SAY NAME1 @129
		SAY NAME2 @129
	
	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMGWAE.CRE~ ~override~
		SAY NAME1 @130
		SAY NAME2 @130
	
	ADD_SPELL ~%MOD_FOLDER%/srwaterelem4scs/ZGMWCWE.SPL~ 2 6 ~WIZARD_CONJURE_WATER_ELEMENTAL~
		SAY NAME1 @127
		SAY UNIDENTIFIED_DESC @128
		SPRINT spell ~%DEST_RES%~
	COPY ~%MOD_FOLDER%/srwaterelem4scs/ZGMWCWES.ITM~ ~override~
		SAY NAME1 @112
		SAY NAME2 @127
		SAY DESC @128
		WRITE_ASCIIE 0xf6 ~%spell%~ #8
		WRITE_ASCIIE 0x126 ~%spell%~ #8
	
	// add to shops where Conjure Fire Elemental is already available
	COPY_EXISTING_REGEXP GLOB ~.*\.sto~ ~override~
		READ_LONG 0x034 ofs_items_for_sales
		READ_LONG 0x038 num_items_for_sales
		FOR (idx = 0; idx < num_items_for_sales; ++idx) BEGIN
	  		SET offset = ofs_items_for_sales + idx * 0x1c
	  		READ_ASCII offset itemname (8) NULL
	  		PATCH_IF (~%itemname%~ STR_EQ ~SCRL7X~) BEGIN
				ADD_STORE_ITEM ~ZGMWCWES~ AFTER ~SCRL7X~ #0 #0 #0 ~IDENTIFIED~ #2
			END
		END
	BUT_ONLY